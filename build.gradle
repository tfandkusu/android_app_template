// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath libs.android.gradle
        classpath libs.kotlin.gradle
        classpath libs.spotless.gradle
        classpath libs.hilt.gradle
        classpath libs.serialization.gradle
        classpath libs.firebase.appdistribution
        classpath libs.deploygate
        classpath libs.oss.licenses
    }
}

allprojects {
    apply plugin: "com.diffplug.spotless"
    spotless {
        ratchetFrom 'origin/main'
        kotlin {
            target '**/*.kt'
            targetExclude('**/*Dao.kt')
            ktlint("0.47.1").setUseExperimental(true)
        }
    }
}

allprojects {
    apply plugin: 'jacoco'
}

task jacocoReport(
        type: JacocoReport,
        dependsOn: subprojects.findAll { project ->
            project.file("build.gradle").exists()
        }.collect { project ->
            "$project.path:testDebugUnitTest"
        }
)
{
    reports {
        xml.required.set(true)
        html.required.set(true)
    }

    def mySourceDirectories = subprojects.collect { project ->
        "${project.projectDir}/src/main/java"
    }
    def myClassDirectories = subprojects.collect { project ->
        "${project.buildDir}/tmp/kotlin-classes/debug"
    }
    sourceDirectories.setFrom(files(mySourceDirectories))
    classDirectories.setFrom(files(myClassDirectories))
    executionData.setFrom(fileTree(dir: rootDir, includes: [
            "**/testDebugUnitTest.exec"
    ]))
}
